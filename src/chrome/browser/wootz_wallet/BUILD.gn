# Copyright (c) 2019 The Wootz Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//chrome/browser/ethereum_remote_client/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")
import("//testing/test.gni")

source_set("wootz_wallet") {
  sources = [
    "asset_ratio_service_factory.cc",
    "asset_ratio_service_factory.h",
    "blockchain_images_source.cc",
    "blockchain_images_source.h",
    "wootz_wallet_context_utils.cc",
    "wootz_wallet_context_utils.h",
    "wootz_wallet_ipfs_service_factory.cc",
    "wootz_wallet_ipfs_service_factory.h",
    "wootz_wallet_service_factory.cc",
    "wootz_wallet_service_factory.h",
    "meld_integration_service_factory.cc",
    "meld_integration_service_factory.h",
    "simulation_service_factory.cc",
    "simulation_service_factory.h",
    "swap_service_factory.cc",
    "swap_service_factory.h",
  ]
  if (!is_android) {
    sources += [ "wallet_notification_helper.h" ]
  }
  deps = [
    "//chrome/components/wootz_wallet/browser",
    "//chrome/components/wootz_wallet/browser:constants",
    "//chrome/components/wootz_wallet/browser:pref_names",
    "//chrome/components/wootz_wallet/browser:utils",
    "//chrome/components/wootz_wallet/common",
    "//chrome/components/wootz_wallet/common:mojom",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/profiles:profiles",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//services/network/public/cpp",
    "//third_party/abseil-cpp:absl",
  ]
  if (enable_extensions) {
    deps += [
      "//chrome/browser/extensions",
      "//chrome/browser/extensions:extensions",
      "//extensions/browser",
    ]
  }
}

source_set("wootz_wallet_delegate") {
  sources = [
    "wootz_wallet_provider_delegate_impl.cc",
    "wootz_wallet_provider_delegate_impl.h",
    "wootz_wallet_service_delegate_base.cc",
    "wootz_wallet_service_delegate_base.h",
    "wootz_wallet_service_delegate_helper.cc",
  ]
  deps = [
    "//chrome/browser/wootz_wallet",
    "//chrome/browser/wootz_wallet:tab_helper",
    "//chrome/components/wootz_wallet/browser",
    "//chrome/components/wootz_wallet/browser:constants",
    "//chrome/components/wootz_wallet/browser:permission_utils",
    "//chrome/components/wootz_wallet/browser:utils",
    "//chrome/components/wootz_wallet/common:mojom",
    "//chrome/components/resources:strings_grit",
    "//chrome/browser/profiles:profile",
    "//components/content_settings/core/common",
    "//components/permissions",
    "//components/permissions:permissions",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
  ]
  if (is_android) {
    sources += [
      "wootz_wallet_provider_delegate_impl_helper_android.cc",
      "wootz_wallet_service_delegate_impl_android.cc",
      "wootz_wallet_service_delegate_impl_android.h",
    ]
    deps += [
      "//chrome/build/android:jni_headers",
      "//chrome/browser/ui:tab_model_enums_java",
      "//chrome/browser/ui:ui",
    ]
  } else {
    sources += [
      "wootz_wallet_provider_delegate_impl_helper.cc",
      "wootz_wallet_service_delegate_impl.cc",
      "wootz_wallet_service_delegate_impl.h",
    ]
    deps += [ "//chrome/browser/ui" ]
    if (enable_extensions) {
      deps += [ ":external_wallets_importer" ]
    }
  }
}

source_set("external_wallets_importer") {
  sources = [
    "external_wallets_importer.cc",
    "external_wallets_importer.h",
  ]
  deps = [
    "//chrome/browser/ethereum_remote_client/buildflags",
    "//chrome/components/wootz_wallet/browser",
    "//chrome/components/wootz_wallet/common",
    "//chrome/components/wootz_wallet/common:mojom",
    "//third_party/argon2",
    "//components/value_store:value_store",
  ]
  if (enable_extensions) {
    deps += [
      "//extensions/browser",
      "//extensions/common",
    ]
  }
  if (ethereum_remote_client_enabled) {
    deps += [
      "//chrome/browser/ethereum_remote_client",
      "//chrome/browser/ethereum_remote_client:browser",
    ]
  }
}

source_set("tab_helper") {
  sources = [
    "wootz_wallet_tab_helper.cc",
    "wootz_wallet_tab_helper.h",
  ]

  deps = [
    "//chrome/common",
    "//chrome/components/wootz_wallet/browser:permission_utils",
    "//components/permissions",
    "//components/sessions",
    "//content/public/browser",
  ]

  if (!is_android) {
    deps += [ "//chrome/browser/ui/wootz_wallet" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "asset_discovery_manager_unittest.cc",
    "asset_discovery_task_unittest.cc",
    "blockchain_images_source_unittest.cc",
    "wootz_wallet_service_unittest.cc",
    "eth_allowance_manager_unittest.cc",
    "eth_pending_tx_tracker_unittest.cc",
    "filecoin_keyring_unittest.cc",
    "keyring_service_unittest.cc",
  ]
  deps = [
    "//base/test:test_support",
    "//chrome/browser/wootz_wallet",
    "//chrome/browser/wootz_wallet:tab_helper",
    "//chrome/common",
    "//chrome/components/wootz_wallet/browser",
    "//chrome/components/wootz_wallet/browser:constants",
    "//chrome/components/wootz_wallet/browser:hd_keyring",
    "//chrome/components/wootz_wallet/browser:permission_utils",
    "//chrome/components/wootz_wallet/browser:pref_names",
    "//chrome/components/wootz_wallet/browser:test_support",
    "//chrome/components/wootz_wallet/browser:transaction",
    "//chrome/components/wootz_wallet/browser:utils",
    "//chrome/components/wootz_wallet/browser/test:test_support",
    "//chrome/components/wootz_wallet/common",
    "//chrome/components/wootz_wallet/common:mojom",
    "//chrome/components/wootz_wallet/common:test_support",
    "//chrome/components/filecoin/rs:rust_lib",
    "//chrome/components/l10n/common",
    "//chrome/components/resources:strings_grit",
    "//chrome/components/version_info",
    "//chrome/browser",
    "//chrome/browser/permissions",
    "//chrome/test:test_support",
    "//components/country_codes",
    "//testing/gtest",
  ]
  if (!is_android) {
    sources += [
      "wootz_wallet_tab_helper_unittest.cc",
      "ethereum_provider_impl_unittest.cc",
      "notifications/wallet_notification_service_unittest.cc",
      "solana_provider_impl_unittest.cc",
    ]
    deps += [
      ":wootz_wallet_delegate",
      "//chrome/components/wootz_wallet/common",
      "//chrome/components/wootz_wallet/common:common_constants",
    ]
    if (enable_extensions) {
      sources += [ "external_wallets_importer_unittest.cc" ]
      deps += [ ":external_wallets_importer" ]
    }
  }
}

source_set("browser_tests") {
  testonly = true
  if (!is_android) {
    sources = [ "wootz_wallet_policy_browsertest.cc" ]

    deps = [
      ":wootz_wallet",
      "//base/test:test_support",
      "//chrome/app:command_ids",
      "//chrome/browser",
      "//chrome/components/wootz_wallet/browser:pref_names",
      "//chrome/components/wootz_wallet/common",
      "//chrome/components/wootz_wallet/common:pref_names",
      "//chrome/components/sidebar/browser",
      "//chrome/browser/ui",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/policy/core/browser",
      "//components/policy/core/common:test_support",
      "//components/prefs:prefs",
      "//components/user_prefs:user_prefs",
      "//content/test:test_support",
      "//testing/gtest",
      "//url",
    ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}  # source_set("browser_tests")
